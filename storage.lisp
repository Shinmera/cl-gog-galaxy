(in-package #:org.shirakumo.fraf.gog-galaxy)

(defclass shared-file ()
  ((id :initarg :id :reader id)))

(defmethod file-name ((file shared-file))
  (gog istorage-get-shared-file-name (handle 'storage) (id file)))

(defmethod file-size ((file shared-file))
  (gog istorage-get-shared-file-size (handle 'storage) (id file)))

(defmethod owner ((file shared-file))
  (ensure-user (gog istorage-get-shared-file-owner (handle 'storage) (id file))))

(defmethod get-file ((file shared-file) (path pathname) &key)
  (let ((interface (handle 'storage))
        (id (id file)))
    (with-open-file (stream path :direction :output :if-exists :supersede :element-type '(unsigned-byte 8))
      (with-listener* (listener)
            (gog istorage-download-shared-file interface id listener)
        (shared-file-download-success (r-id name)
          (when (= id r-id)
            (let ((buf (make-array 4096 :element-type '(unsigned-byte 8)))
                  (max (file-size file)))
              (cffi:with-pointer-to-vector-data (ptr buf)
                (loop for start from 0 below max
                      for read = (gog:istorage-shared-file-read interface id ptr 4096 start)
                      do (incf start read)
                         (write-sequence buf stream :end read))))
            (return-from listener path)))
        (shared-file-download-failure (r-id failure)
          (when (= id r-id)
            (gog-error failure)))))))

(define-interface storage gog:storage
  (put-file ((path pathname) &key (name (file-namestring path)))
    (with-open-file (stream path :direction :input :element-type '(unsigned-byte 8))
      (let* ((size (file-length stream))
             (buf (make-array size :element-type '(unsigned-byte 8))))
        (read-sequence buf stream)
        (cffi:with-pointer-to-vector-data (ptr buf)
          (gog istorage-file-write handle name ptr size))))
    path)

  (get-file ((path pathname) &key (name (file-namestring path)))
    (let* ((size (gog istorage-get-file-size handle name))
           (buf (make-array size :element-type '(unsigned-byte 8))))
      (cffi:with-pointer-to-vector-data (ptr buf)
        (when (< (gog istorage-file-read handle name ptr size) size)
          (gog-error NIL "Failed to read file: too small!")))
      (with-open-file (stream path :direction :output :if-exists :supersede :element-type '(unsigned-byte 8))
        (write-sequence buf stream))
      (values path (to-universal-time (gog istorage-get-file-timestamp handle name)))))

  (list-files (&key)
    (loop for i from 0 below (gog istorage-get-file-count handle)
          collect (gog istorage-get-file-name-by-index handle i)))

  (file-exists-p (name)
    (gog istorage-file-exists handle name))

  (remove-file (name &key)
    (gog istorage-file-delete handle name))

  (share-file (name)
    (with-listener* (listener)
          (gog:istorage-file-share handle name listener)
      (file-share-success (r-name id)
        (when (string= name r-name)
          (return-from listener (make-instance 'shared-file :id id))))
      (file-share-failure (r-name failure)
        (when (string= name r-name)
          (gog-error failure)))))

  (list-shared-files ()
    (loop for i from 0 below (gog istorage-get-downloaded-shared-file-count handle)
          collect (make-instance 'shared-file :id (gog istorage-get-downloaded-shared-file-by-index handle i)))))
